apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'de.mannodermaus.android-junit5'

def roomSchemasDir = new File(projectDir, "room/schemas")
def androidTestRoomSchemasDir = new File(buildDir, "tmp/room/schemas/androidTest")
def copyRoomSchemasTask = tasks.register("copyRoomSchemasToAndroidTestAssets", Sync.class) {
	from(roomSchemasDir)
	into(androidTestRoomSchemasDir)
}

android {
	def globalConfiguration = rootProject.extensions.getByName("ext")

	compileSdkVersion globalConfiguration["androidCompileSdkVersion"]
	buildToolsVersion globalConfiguration["androidBuildToolsVersion"]

	defaultConfig {
		minSdkVersion globalConfiguration["androidMinSdkVersion"]
		targetSdkVersion globalConfiguration["androidTargetSdkVersion"]

		buildConfigField 'int', 'VERSION_CODE', "${globalConfiguration["androidVersionCode"]}"
		buildConfigField "String", "VERSION_NAME", "\"${globalConfiguration["androidVersionName"]}\""

		testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'

		javaCompileOptions {
			annotationProcessorOptions {
				compilerArgumentProviders(
						new RoomSchemaArgProvider(roomSchemasDir)
				)
			}
		}
	}

	compileOptions {
		sourceCompatibility JavaVersion.VERSION_17
		targetCompatibility JavaVersion.VERSION_17

		coreLibraryDesugaringEnabled true
	}

	buildTypes {
		release {
			buildConfigField "String", "ONEDRIVE_API_KEY", "\"" + getApiKey('ONEDRIVE_API_KEY') + "\""
			buildConfigField "String", "ONEDRIVE_API_REDIRCT_URI", "\"" + getApiKey('ONEDRIVE_API_REDIRCT_URI') + "\""
		}

		debug {
			buildConfigField "String", "ONEDRIVE_API_KEY", "\"" + getApiKey('ONEDRIVE_API_KEY_DEBUG') + "\""
			buildConfigField "String", "ONEDRIVE_API_REDIRCT_URI", "\"" + getApiKey('ONEDRIVE_API_REDIRCT_URI_DEBUG') + "\""
		}
	}

	flavorDimensions "version"

	productFlavors {
		playstore {
			dimension "version"
		}

		apkstore {
			dimension "version"
		}

		fdroid {
			dimension "version"
		}

		lite {
			dimension "version"
		}
	}

	sourceSets {
		playstore {
			java.srcDirs = ['src/main/java/', 'src/apiKey/java/', 'src/apkStorePlaystore/java/']
		}

		apkstore {
			java.srcDirs = ['src/main/java/', 'src/apiKey/java/', 'src/apkStorePlaystore/java/']
		}

		fdroid {
			java.srcDirs = ['src/main/java/', 'src/apiKey/java/', 'src/fdroid/java/']
		}

		lite {
			java.srcDirs = ['src/main/java/', 'src/lite/java/']
		}

		androidTest.assets.srcDir(androidTestRoomSchemasDir)
	}
	packagingOptions {
		resources {
			excludes += ['META-INF/DEPENDENCIES', 'META-INF/NOTICE.md', 'META-INF/INDEX.LIST']
		}
	}
	libraryVariants.configureEach { variant ->
		tasks.findByName("kapt${variant.name.capitalize()}Kotlin")?.configure { kaptTask ->
			copyRoomSchemasTask.configure { it.mustRunAfter(kaptTask) }
		}
		tasks.findByName("kapt${variant.name.capitalize()}AndroidTestKotlin")?.configure { kaptTask ->
			copyRoomSchemasTask.configure { it.mustRunAfter(kaptTask) }
		}
		tasks.findByName("merge${variant.name.capitalize()}AndroidTestAssets")?.configure {
			it.dependsOn(copyRoomSchemasTask)
		}
	}

	lint {
		abortOnError false
		ignoreWarnings true
		quiet true
	}
	namespace 'org.cryptomator.data'
}

configurations.all {
	// Check for updates every build (use for cryptolib snapshot)
	//resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

dependencies {
	def dependencies = rootProject.ext.dependencies

	implementation project(':domain')
	implementation project(':util')
	playstoreImplementation dependencies.pcloud
	apkstoreImplementation dependencies.pcloud
	fdroidImplementation dependencies.pcloud

	coreLibraryDesugaring dependencies.coreDesugaring

	// cryptomator
	implementation dependencies.cryptolib

	// room
	implementation dependencies.room
	annotationProcessor dependencies.roomCompiler
	kapt dependencies.roomCompiler

	// dagger
	annotationProcessor dependencies.daggerCompiler
	kapt dependencies.daggerCompiler
	implementation dependencies.dagger

	implementation dependencies.jsonWebToken

	// cloud
	playstoreImplementation dependencies.dropboxCore
	playstoreImplementation dependencies.dropboxAndroid
	apkstoreImplementation dependencies.dropboxCore
	apkstoreImplementation dependencies.dropboxAndroid
	fdroidImplementation dependencies.dropboxCore
	fdroidImplementation dependencies.dropboxAndroid


	playstoreImplementation dependencies.msgraphAuth
	apkstoreImplementation dependencies.msgraphAuth
	fdroidImplementation dependencies.msgraphAuth
	playstoreImplementation dependencies.msgraph
	apkstoreImplementation dependencies.msgraph
	fdroidImplementation dependencies.msgraph

	implementation dependencies.stax
	api dependencies.minIo

	playstoreImplementation(dependencies.googlePlayServicesAuth) {
		exclude module: 'guava-jdk5'
		exclude module: 'httpclient'
		exclude module: 'googlehttpclient'
		exclude group: "com.google.http-client", module: "google-http-client"
	}
	apkstoreImplementation(dependencies.googlePlayServicesAuth) {
		exclude module: 'guava-jdk5'
		exclude module: 'httpclient'
		exclude module: "google-http-client"
		exclude group: "com.google.http-client", module: "google-http-client"
	}

	playstoreImplementation(dependencies.googleApiServicesDrive) {
		exclude module: 'guava-jdk5'
		exclude module: 'httpclient'
		exclude module: 'googlehttpclient'
		exclude group: "com.google.http-client", module: "google-http-client"
	}
	apkstoreImplementation(dependencies.googleApiServicesDrive) {
		exclude module: 'guava-jdk5'
		exclude module: 'httpclient'
		exclude module: "google-http-client"
		exclude group: "com.google.http-client", module: "google-http-client"
	}

	playstoreImplementation(dependencies.googleApiClientAndroid) {
		exclude module: 'guava-jdk5'
		exclude module: 'httpclient'
		exclude module: "google-http-client"
		exclude module: "jetified-google-http-client"
		exclude group: "com.google.http-client", module: "google-http-client"
		exclude group: "com.google.http-client", module: "jetified-google-http-client"
	}
	apkstoreImplementation(dependencies.googleApiClientAndroid) {
		exclude module: 'guava-jdk5'
		exclude module: 'httpclient'
		exclude module: "google-http-client"
		exclude module: "jetified-google-http-client"
		exclude group: "com.google.http-client", module: "google-http-client"
		exclude group: "com.google.http-client", module: "jetified-google-http-client"
	}

	playstoreImplementation dependencies.trackingFreeGoogleCLient
	apkstoreImplementation dependencies.trackingFreeGoogleCLient
	playstoreImplementation dependencies.trackingFreeGoogleAndroidCLient
	apkstoreImplementation dependencies.trackingFreeGoogleAndroidCLient

	// rest
	implementation dependencies.rxJava
	implementation dependencies.rxAndroid
	implementation dependencies.okHttp
	implementation dependencies.okHttpDigest
	implementation dependencies.androidAnnotations
	compileOnly dependencies.javaxAnnotation
	implementation dependencies.gson

	implementation dependencies.commonsCodec

	implementation dependencies.documentFile

	implementation dependencies.lruFileCache

	implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

	// test
	testImplementation dependencies.junit
	testImplementation dependencies.junitApi
	testRuntimeOnly dependencies.junitEngine
	testImplementation dependencies.junitParams
	testRuntimeOnly dependencies.junit4Engine
	implementation dependencies.androidxTestJunitKtln

	testImplementation dependencies.mockito
	testImplementation dependencies.mockitoKotlin
	testImplementation dependencies.mockitoInline
	testImplementation dependencies.hamcrest

	androidTestImplementation dependencies.roomTesting
	androidTestImplementation(dependencies.runner) {
		exclude group: 'com.android.support', module: 'support-annotations'
	}
}

configurations {
	all*.exclude group: 'com.google.android', module: 'android'
}

tasks.withType(Test) {
	testLogging {
		events "failed"

		showExceptions true
		exceptionFormat "full"
		showCauses true
		showStackTraces true

		showStandardStreams = false
	}
}

class RoomSchemaArgProvider implements CommandLineArgumentProvider {

	@InputDirectory
	@PathSensitive(PathSensitivity.RELATIVE)
	File schemaDir

	RoomSchemaArgProvider(File schemaDir) {
		this.schemaDir = schemaDir
	}

	@Override
	Iterable<String> asArguments() {
		return ["-Aroom.schemaLocation=${schemaDir.path}".toString()]
	}
}